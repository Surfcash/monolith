buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.25"
    }
}

subprojects {
    group = 'gg.warcraft'
    version = '2.0.0-SNAPSHOT'

    apply plugin: 'scala'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.maiflai.scalatest'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compile 'org.scala-lang:scala-library:2.12.8' // TODO update to 2.13
        compile 'com.beachape:enumeratum_2.12:1.5.13'

        testCompile 'junit:junit:4.12' // TODO remove
        testCompile 'org.mockito:mockito-core:2.18.3' // TODO remove
        testCompile 'org.apache.commons:commons-lang3:3.3.1' // TODO rem.
        testCompile 'org.scalatest:scalatest_2.12:3.0.7'
        testCompile 'org.scalamock:scalamock-scalatest-support_2.12:3.6.0'

        testRuntime 'org.pegdown:pegdown:1.6.0'
    }

    sourceSets {
        main {
            scala {
                srcDirs = ['src/main/scala', 'src/main/java']
            }
            java {
                srcDirs = []
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
    }

    tasks.withType(ScalaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
    }

    publishing {
        publications {
            java(MavenPublication) {
                from components.java
            }
        }
    }

    publishing {
        publications {
            shadow(MavenPublication) {
                project.shadow.component(it)
            }
        }
    }
}
